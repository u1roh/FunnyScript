# FunnyScript

* F# で作ったスクリプト言語です。ド素人が勉強のために作った習作なので、ヘボいです。
* 開発環境は Visual Studio 2015 です。
* パーザーは FParsec を利用しています。
* ろくに勉強しないで勘で作ったので、ツッコミどころ満載だと思います。お手柔らかにお願いします。
* 関数型です（たぶん、そのつもり）。式ベースで、文はありません。
* 動的型付きのスクリプト言語です。静的型付き言語を作る能力は私にはありません＞＜
* 正格評価です。遅延評価はしません。
* これといって特筆すべき特長はありません。
* キーワード（予約語）がない言語にしてみようかなと思っていましたが、結局 do キーワードだけは存在しているという中途半端っぷりです。
* 文法を間違えると意味不明なエラーメッセージが出るのでデバッグ不能です。
* とても実用できる完成度ではありません。オモチャです。

オモチャですがとりあえず「言語を自作したことがあるか」という質問に Yes と答えても良さそうになったので僕は大変満足です、はい。
自分で実際に作ってみると「既存の言語は実によく出来ているんだなぁ」と感じることしばしばで、プログラミング言語に対する感謝と畏敬の念が育まれました。

## 文法

### Hello World
```
do System.Console.WriteLine "Hello World";
```

### コメント
```
// コメントです
```

### リテラル
```
123     // 整数値
3.14    // 浮動小数点数
"hello" // 文字列
true    // 真
false   // 偽
()      // null
(a, b)  // タプル
[a, b]  // リスト
```

文字列リテラルはバックスラッシュによるエスケープ処理とか未実装なのでダメダメです＞＜

### 変数束縛
```
a := 1;
b := 3.14;
c := "hello";
```

行末にセミコロンがついていると「文」っぽいですが、これは F# や OCaml などの `in` に相当する役割です。

### 関数定義（ラムダ式）
```
add10 := x -> x + 10; // x に10を足す関数
a := add10 20;  // 呼び出し

add := x -> y -> x + y; // x + y を計算する関数（カリー化）
a := add 10 20;  // 呼び出し

add := (x, y) -> x + y; // x + y を計算する関数（タプル）
a := add (10, 20);  // 呼び出し

hello := () -> System.Console.WriteLine "Hello World"  // 引数なしの関数
do hello ();  // 呼び出し
```

ラムダ式以外に関数を定義する文法はありません。

### 条件分岐
```
result := ? x % 2 == 0 => "even" | "odd"
```

if 式なのですけれど、記号だけの変な文法にしてみました。
* `?` が if に相当します。
* `=>` が then に相当します。
* `|` が else に相当します。

特別ルールとして、`?` の前に `|` を付けて `|?` と書いてもOKです。
従って次のように書けます。
```
result :=
  |? x % 2 == 0 => "even"
  | "odd"
```
いわゆる `else if` も `|?` と書けます。
```
fizzbuzz := n ->
  |? n % 3 == 0 && n % 5 == 0 => "FizzBuzz"
  |? n % 3 == 0 => "Fizz"
  |? n % 5 == 0 => "Buzz"
  | n;
```

### 再帰呼び出し
```
fac := n -> ? n == 0 => 1 | n * fac (n - 1);	// 階乗計算
```

しれっと再帰呼び出ししていますが、内部はやや強引な実装になっています。

`:=` で変数束縛が生じたときに、その値が関数である場合に限って、その変数（つまり関数）に関数内部から参照できるように特別処理されています。具体的に言うと、その関数が内部からアクセスできる「環境」にその変数を後からねじ込んでいる感じです。

この言語では関数定義はラムダ式しか用意していないので、再帰呼び出しを可能にするにはこういう風にするしかないよなぁと思っているのですが…。

### 末尾再帰最適化
出来ません＼(^o^)／

作り方分かりません。教えてエラい人。

### レコード
```
a := { name := "u1roh"; age := 40; };
do System.Console.WriteLine ("name = {0}, age = {1}", a.name, a.age);
```
（↑どーも、40のおじさんです）

この応用でモジュールっぽいものが作れます。

```
Calc := {
  add := x -> y -> x + y;
  sub := x -> y -> x - y;
};
do System.Console.WriteLine ("add: {0}", Calc.add 7 9);
do System.Console.WriteLine ("sub: {0}", Calc.sub 8 2);
```

### リスト
```
a := [1, 2, 3];  // リテラル
b := List.init 3 (i -> 2 * i);  // [0, 2, 4]
c := a.head;  // 先頭要素（この場合は 1）
d := a.tail;  // 2番目以降（この場合は [2, 3])
len := a.length; // 3
a1 := a 1;  // 2（インデックスによる要素取得）
e := 10 :: a; // [10, 1, 2, 3] （cons 演算子）
```

配列とリストの区別はありません。

### パイプライン演算子
F#er としては外せない演算子です。
```
add := x -> y -> x + y;
a := 10 |> add 5; // 15
```

### 繰り返し
構文ありません。再帰で書け！ただし末尾再帰最適化ないけどな！はっはっは。

コード例：
```
// リストに対して繰り返しを実行する関数
iter := f -> ls ->
	? ls.isEmpty => () | do f ls.head; iter f ls.tail;

// リストの全要素を表示  
do [9, 8, 7, 6] |> iter System.Console.WriteLine;
```


### .NET Framework の呼び出し
既にサンプル中に `System.Console.WriteLine` の呼び出しは登場しています。
同様に他の関数も呼び出せます。

```
a := System.Math.Abs -1; // 1
b := System.Math.Sin 3.14
```

クラス名後に `.new` を付けるとコンストラクタが呼び出せます。

```
stack := System.Collections.Stack.new();
do stack.Push 123;
do stack.Push 456;
n := stack.Count;
```

↑ジェネリッククラスは使えないので、前時代の遺物である非ジェネリックなコレクションクラスを使います。
